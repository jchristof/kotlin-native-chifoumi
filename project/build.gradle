plugins {
    id 'kotlin-multiplatform' version '1.3.60'
    id 'org.hidetake.ssh' version '2.10.1'
}

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    linuxArm32Hfp("chifoumi") {
        binaries {
            executable {
                entryPoint "chifoumi.robot.main"

                // libpigpio.so are compiled on raspberry pi and copied here
                linkerOpts("-Lsrc/lib/pigpio", "-lpigpio")

                // libPCA9685.so are compiled on raspberry pi and copied here
                linkerOpts("-Lsrc/lib/pca9685", "-lpca9685")

                linkerOpts("-Lsrc/lib/tensorflow", "-ltensorflow")
            }
        }

        compilations.main.cinterops {
            pigpio {
                includeDirs 'src/include/pigpio'
            }
            pca9685 {
                includeDirs 'src/include/pca9685'
            }
            tensorflow {
                includeDirs 'src/include/tensorflow'
            }
        }
    }
}

remotes {
    pi {
        // your raspberry pi IP address
        host = '192.168.1.98'
        user = 'pi'
        identity = file("${System.properties['user.home']}/.ssh/raspberry_pi")
    }
}

task deployOnPi {
    doLast {
        ssh.run {
            session(remotes.pi) {
                def sourceFile = "$buildDir/bin/chifoumi/releaseExecutable/chifoumi-robot.kexe"
                def destinationDir = "/home/pi/qian/playground"

                put from: sourceFile, into: destinationDir
            }
        }
    }
}
