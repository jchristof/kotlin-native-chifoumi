plugins {
    id 'kotlin-multiplatform' version '1.3.50'
    id 'org.hidetake.ssh' version '2.10.1'
}

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    linuxArm32Hfp("chifumi") {
        binaries {
            executable {
                entryPoint "chifumi.robot.main"

                // libpigpio.so are compiled on raspberry pi and copied here
                linkerOpts("-Lsrc/include/pigpio", "-lpigpio")

                // libPCA9685.so are compiled on raspberry pi and copied here
                linkerOpts("-Lsrc/include/pca9685", "-lpca9685")
            }
        }

        compilations.main.cinterops {
            pigpio {
                includeDirs 'src/include/pigpio'
            }
            pca9685 {
                includeDirs 'src/include/pca9685'
            }
        }
    }
}

remotes {
    pi {
        // your raspberry pi IP address
        host = '192.168.1.98'
        user = 'pi'
        identity = file("${System.properties['user.home']}/.ssh/raspberry_pi")
    }
}

task deployOnPi {
    doLast {
        ssh.run {
            session(remotes.pi) {
                def sourceFile = "$buildDir/bin/chifumi/releaseExecutable/chifumi-robot.kexe"
                def destinationDir = "/home/pi/qian/playground"

                put from: sourceFile, into: destinationDir
            }
        }
    }
}
